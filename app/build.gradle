plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow'
    id 'org.jetbrains.kotlin.jvm'
}

repositories { mavenCentral() }

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
}
kotlin {
    jvmToolchain(21)
}

dependencies {
    // --- Test ---
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // --- Kotlin (BOMでバージョンを揃える) ---
    implementation platform("org.jetbrains.kotlin:kotlin-bom:2.0.20")
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'

    // --- JSON: KotlinフレンドリーなJacksonセット ---
    implementation platform('com.fasterxml.jackson:jackson-bom:2.17.1')
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'com.fasterxml.jackson.module:jackson-module-afterburner'

    implementation libs.guava
    implementation 'org.apache.kafka:kafka-clients:3.5.1'
    implementation 'org.hdrhistogram:HdrHistogram:2.1.12'

    // --- Prometheus: メトリクスエクスポート ---
    implementation 'io.prometheus:simpleclient:0.16.0'
    implementation 'io.prometheus:simpleclient_common:0.16.0'

    // --- HFT: 低レイテンシライブラリ ---
    implementation 'com.lmax:disruptor:3.4.4'        // ロックフリーRing Buffer
    implementation 'org.agrona:agrona:1.21.1'        // Zero-GCコレクション
    implementation 'net.openhft:chronicle-queue:5.25ea5'  // 永続化ジャーナル (Java 21対応)
    implementation 'ch.qos.logback:logback-classic:1.5.6'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    implementation 'io.etcd:jetcd-core:0.7.7'
    implementation 'io.etcd:jetcd-common:0.7.7'

    // --- Market Data: 外部APIクライアント ---
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = '21'
    }
}

tasks.named('shadowJar') {
    archiveBaseName.set('app')
    archiveClassifier.set('all')
    mergeServiceFiles()
}

application {
    mainClass.set((findProperty('mainClass') ?: "app.MainKt") as String)

    applicationDefaultJvmArgs = [
        // JDK module access (Chronicle Queue, Agrona)
        '--add-exports=java.base/jdk.internal.ref=ALL-UNNAMED',
        '--add-exports=java.base/sun.nio.ch=ALL-UNNAMED',
        '--add-exports=jdk.unsupported/sun.misc=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED',
        '--add-opens=jdk.compiler/com.sun.tools.javac=ALL-UNNAMED',
        '--add-opens=java.base/java.lang=ALL-UNNAMED',
        '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED',
        '--add-opens=java.base/java.io=ALL-UNNAMED',
        '--add-opens=java.base/java.util=ALL-UNNAMED',

        // Heap size
        '-Xms4g',
        '-Xmx4g',

        // Agrona optimization
        '-Dagrona.disable.bounds.checks=true',

        // Chronicle Queue optimization
        '-Dchronicle.queue.check.interrupts=false'
    ]
}

jar {
    manifest { attributes('Main-Class': application.mainClass.get()) }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('test') { useJUnitPlatform() }
