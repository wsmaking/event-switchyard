plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm'
    id 'com.github.johnrengelman.shadow'
    id 'nu.studer.jooq' version '9.0'
}

repositories { mavenCentral() }

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
}
kotlin {
    jvmToolchain(21)
}

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated-src/jooq/main"
        }
    }
}

dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation platform("org.jetbrains.kotlin:kotlin-bom:2.0.20")
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'

    implementation platform('com.fasterxml.jackson:jackson-bom:2.17.1')
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    implementation 'ch.qos.logback:logback-classic:1.5.6'
    implementation 'org.apache.kafka:kafka-clients:3.5.1'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.postgresql:postgresql:42.7.3'
    implementation 'org.jooq:jooq:3.19.10'

    jooqGenerator 'org.jooq:jooq-meta-extensions:3.19.10'
    jooqGenerator 'com.h2database:h2:2.2.224'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = '21'
    }
}

tasks.named('shadowJar') {
    archiveBaseName.set('backoffice')
    archiveClassifier.set('all')
    mergeServiceFiles()
}

jooq {
    version = '3.19.10'
    configurations {
        main {
            generationTool {
                jdbc {
                    driver = 'org.h2.Driver'
                    url = 'jdbc:h2:mem:backoffice'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.extensions.ddl.DDLDatabase'
                        properties {
                            property {
                                key = 'scripts'
                                value = 'src/main/resources/db/migration/V1__backoffice_schema.sql'
                            }
                            property {
                                key = 'sort'
                                value = 'semantic'
                            }
                            property {
                                key = 'dialect'
                                value = 'POSTGRES'
                            }
                        }
                    }
                    target {
                        packageName = 'backoffice.jooq'
                        directory = "${buildDir}/generated-src/jooq/main"
                    }
                }
            }
        }
    }
}

tasks.named('compileKotlin') {
    dependsOn tasks.named('generateJooq')
}

application {
    mainClass.set((findProperty('mainClass') ?: "backoffice.MainKt") as String)
}

jar {
    manifest { attributes('Main-Class': application.mainClass.get()) }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('test') { useJUnitPlatform() }
